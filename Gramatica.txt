

<Programa>:= {<Declaracion>}+

<Declaracion>:= 
      <Principal>
    | <DecVariable>
    | <DecConstante>

<Principal>:= INICIO
                {<Sentencia>}+
                FIN

<Sentencia>:= 
      <Escribir>
    | <Leer>
    | <Control> 
    | <DecVariable>
    | <DecConstante>
    | <Asignacion>

<Asignacion> := <Variable> = <Expresion>;

<Escribir>:= ESCRIBIR'('<Expresion>')';
<Leer>:= LEER <Variable>;

<Control>:= 
      <Condicional>
    | <Ciclo>

<Condicional>:= SI '('<Expresion>')'
                  {Sentencia}+
                [SINO 
                  {Sentencia}+]
                FSI

<Ciclo>:= MIENTRAS'('<Expresion>')'
            {<Sentencia>}+
          FMIENTRAS

<DecVariable>:= <Tipo> <Identificador> [= <Expresion>];
<DecConstante>:= CONSTANTE <Identificador> = <Expresion>;

<Expresion>:=
      <Logico>
    | <Binario>
    | <Unario>
    | <Literal>
    | <Grupo>

<Logico>:= <Expresion> (Y|O) <Expresion>

<Binario>:= <Expresion> (+|-|*|/|%|==|!=|>|>=|<|<=) <Expresion>

<Unario>:= (!|-)<Expresion>

<Literal>:= 
      <Cadena>
    | <Caracter>
    | <Entero>
    | <Real>
    | <Booleano>
    | <Identificador>

<Grupo>:= '('<Expresion>')'


<Tipo>:= 
    | <Primitivo>
    | <Identificador>

<Primitivo>:= (ENTERO | REAL | CADENA | CARACTER | BOOLEANO)


<Cadena>:= "{(<Numero>|<Letra>|<Simbolo>)}*"
<Caracter>:= '(<Numero>|<Letra>|Simbolo)'

<Entero>:= {(0-9)}+
<Real>:= {(0-9)}+ . {(0-9)}+

<Booleano>:= (VERDADERO | FALSO)

<Identificador>:= (<Letra>| _) {(<Letra> | <Numero> | _)}*